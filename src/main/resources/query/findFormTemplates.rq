prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix ft: <http://onto.fel.cvut.cz/ontologies/form-template/>
prefix dc: <http://purl.org/dc/terms/>

CONSTRUCT {
      ?formGraph rdfs:label ?label .
      ?formGraph rdfs:comment ?explanation .
}
WHERE {
    GRAPH ?formGraph {
       ?formRoot a ft:root-entity .
       OPTIONAL {
            ?formGraph skos:prefLabel ?formLabel .
       }
    }
    SERVICE <https://graphdb.onto.fel.cvut.cz/repositories/kodi-slovnik-gov-cz> {
      ?formRoot skos:prefLabel ?entityLabel .
      OPTIONAL {
         ?formRoot dc:description ?explanation .
      }
   }
   BIND(COALESCE(?formLabel, ?entityLabel) as ?label)
}